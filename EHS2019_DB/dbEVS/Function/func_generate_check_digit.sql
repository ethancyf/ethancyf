IF  EXISTS (SELECT 1 FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[func_generate_check_digit]') AND type in (N'FN', N'IF', N'TF', N'FS', N'FT'))
	DROP FUNCTION [dbo].[func_generate_check_digit]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author:			Vincent
-- Create date:	27 JAN 2010
-- Description:	Generate check digit
-- =============================================
-- =============================================
-- Modification History
-- Modified by:			
-- Modified date:			
-- Description:			
-- =============================================
CREATE FUNCTION [dbo].[func_generate_check_digit]
(
	@system_number VARCHAR(50)
)
RETURNS CHAR(1)
AS
BEGIN
	-- =============================================
	-- Declaration
	-- =============================================
	DECLARE @result CHAR(1)

	DECLARE @temp_system_number VARCHAR(50)
	DECLARE @tenth_digit CHAR(1)
	DECLARE @unit_digit CHAR(1)
	DECLARE @current_system_char CHAR(1)

	DECLARE @index INT
	DECLARE @temp_check_value INT
	DECLARE @temp_check_value_string VARCHAR(10)
	DECLARE @sum_check_value INT

	-- =============================================
	-- Validation 
	-- =============================================
	-- =============================================
	-- Initialization
	-- =============================================
	SET @result = ''

	SET @temp_system_number = @system_number
	SET @tenth_digit = ''
	SET @unit_digit = ''
	SET @current_system_char = ''

	SET @temp_check_value = 0
	SET @sum_check_value = 0
	SET @temp_check_value_string = ''
	SET @index = LEN(@temp_system_number)


	-- Generate Check Digit:	The check digit is generated by LUHN formula Mod 10
	WHILE (@index > 0)
	BEGIN
		SET @current_system_char = SUBSTRING(@temp_system_number, @index, 1)

		IF @current_system_char > '9'
			SET @temp_check_value = ASCII(@current_system_char) - 65		-- "65" = ASCII('A')
		ELSE
			SET @temp_check_value = CONVERT(int, @current_system_char )

		IF (@index - 1)%2 = 0	-- (@index - 1) AS the Index is count from 1 to Length of System Number
			SET @temp_check_value = @temp_check_value * 2

		SET @temp_check_value_string = CONVERT(VARCHAR, @temp_check_value )
		WHILE LEN(@temp_check_value_string) > 1
		BEGIN
			SET @tenth_digit  = SUBSTRING(@temp_check_value_string, 1, 1)
			SET @unit_digit = SUBSTRING(@temp_check_value_string, 2, 1)

			SET @temp_check_value = CONVERT(INT, @tenth_digit) + CONVERT(INT, @unit_digit)
			SET @temp_check_value_string = CONVERT(VARCHAR, @temp_check_value )
		END

		SET @sum_check_value = @sum_check_value + @temp_check_value

		SET @index = @index - 1
	END

	SET @result = CONVERT(CHAR, @sum_check_value%10)

	-- =============================================
	-- Return results
	-- =============================================
	RETURN @result

END
GO


Grant execute on [dbo].[func_generate_check_digit] to HCSP
GO

Grant execute on [dbo].[func_generate_check_digit] to HCVU
GO

Grant execute on [dbo].[func_generate_check_digit] to HCPUBLIC
GO 